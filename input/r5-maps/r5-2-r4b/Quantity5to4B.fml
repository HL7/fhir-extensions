/// url = 'http://hl7.org/fhir/StructureMap/Quantity5to4B'
/// name = 'Quantity5to4B'
/// title = 'FML Conversion for Quantity: R5 to R4B'

/// status = 'active'

conceptmap "QuantityComparator" {
  prefix s = "http://hl7.org/fhir/quantity-comparator"
  prefix t = "http://hl7.org/fhir/4.3/quantity-comparator"

  s:"<" - t:"<"
  s:"<=" - t:"<="
  s:">=" - t:">="
  s:">" - t:">"
  s:"ad" - t:"<=" // can't round trip this without an extension
}

uses "http://hl7.org/fhir/StructureDefinition/Quantity" alias QuantityR5 as source
uses "http://hl7.org/fhir/4.3/StructureDefinition/Quantity" alias QuantityR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

group Quantity(source src : QuantityR5, target tgt : QuantityR4B) extends Element <<type+>> {
  src.value -> tgt.value;
  src.comparator as v -> tgt.comparator = translate(v, '#QuantityComparator', 'code');
  src.unit -> tgt.unit;
  src.system -> tgt.system;
  src.code -> tgt.code;
}

