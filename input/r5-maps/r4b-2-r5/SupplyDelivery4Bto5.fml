/// url = 'http://hl7.org/fhir/StructureMap/SupplyDelivery4Bto5'
/// name = 'SupplyDelivery4Bto5'
/// title = 'FML Conversion for SupplyDelivery: R4B to R5'

conceptmap "SupplyDeliveryStatus" {
  prefix s = "http://hl7.org/fhir/4.3/supplydelivery-status"
  prefix t = "http://hl7.org/fhir/supplydelivery-status"

  s:"in-progress" - t:"in-progress"
  s:"completed" - t:"completed"
  s:"abandoned" - t:"abandoned"
  s:"entered-in-error" - t:"entered-in-error"
}

conceptmap "SupplyDeliverySupplyItemType" {
  prefix s = "http://hl7.org/fhir/4.3/supplydelivery-supplyitemtype"
  prefix t = "http://hl7.org/fhir/supplydelivery-supplyitemtype"

  s:"medication" - t:"medication"
  s:"device" - t:"device"
  s:"biologicallyderivedproduct" - t:"biologicallyderivedproduct"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/SupplyDelivery" alias SupplyDeliveryR4B as source
uses "http://hl7.org/fhir/StructureDefinition/SupplyDelivery" alias SupplyDeliveryR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group SupplyDelivery(source src : SupplyDeliveryR4B, target tgt : SupplyDeliveryR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.basedOn -> tgt.basedOn;
  src.partOf -> tgt.partOf;
  src.status as v -> tgt.status = translate(v, '#SupplyDeliveryStatus', 'code');
  src.patient -> tgt.patient;
  src.type as v -> tgt.type = translate(v, '#SupplyDeliverySupplyItemType', 'code');
  src.suppliedItem as s -> tgt.suppliedItem as t then SupplyDeliverySuppliedItem(s,t);
  src.occurrence : dateTime -> tgt.occurrence;
  src.occurrence : Period -> tgt.occurrence;
  src.occurrence : Timing -> tgt.occurrence;
  src.supplier -> tgt.supplier;
  src.destination -> tgt.destination;
  src.receiver -> tgt.receiver;
}

group SupplyDeliverySuppliedItem(source src, target tgt) extends BackboneElement {
  src.quantity -> tgt.quantity;
  src.item : CodeableConcept -> tgt.item;
  src.item : Reference -> tgt.item;
}

