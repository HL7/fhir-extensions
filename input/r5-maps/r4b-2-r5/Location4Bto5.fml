/// url = 'http://hl7.org/fhir/StructureMap/Location4Bto5'
/// name = 'Location4Bto5'
/// title = 'FML Conversion for Location: R4B to R5'

/// status = 'active'

conceptmap "LocationMode" {
  prefix s = "http://hl7.org/fhir/4.3/location-mode"
  prefix t = "http://hl7.org/fhir/location-mode"

  s:"instance" - t:"instance"
  s:"kind" - t:"kind"
}

conceptmap "LocationStatus" {
  prefix s = "http://hl7.org/fhir/4.3/location-status"
  prefix t = "http://hl7.org/fhir/location-status"

  s:"active" - t:"active"
  s:"suspended" - t:"suspended"
  s:"inactive" - t:"inactive"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/Location" alias LocationR4B as source
uses "http://hl7.org/fhir/StructureDefinition/Location" alias LocationR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group Location(source src : LocationR4B, target tgt : LocationR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, '#LocationStatus', 'code');
  src.operationalStatus -> tgt.operationalStatus;
  src.name -> tgt.name;
  src.alias -> tgt.alias;
  src.description -> tgt.description;
  src.mode as v -> tgt.mode = translate(v, '#LocationMode', 'code');
  src.type -> tgt.type;
  src.telecom : ContactPoint as s -> tgt.contact = create('ExtendedContactDetail') as t, t.telecom = s;
  src.address -> tgt.address;
  src.physicalType -> tgt.form;
  src.position as s -> tgt.position as t then LocationPosition(s,t);
  src.managingOrganization -> tgt.managingOrganization;
  src.partOf -> tgt.partOf;
//  src.characteristic -> tgt.characteristic;
  src.hoursOfOperation : BackboneElement as s -> tgt.hoursOfOperation as t then LocationAvailabilityAvailableTime(s,t) "availableTime";
//  src.virtualService -> tgt.virtualService;
  src.endpoint -> tgt.endpoint;
}

group LocationPosition(source src, target tgt) extends BackboneElement {
  src.longitude -> tgt.longitude;
  src.latitude -> tgt.latitude;
  src.altitude -> tgt.altitude;
}

group LocationAvailabilityAvailableTime(source src, target tgt) extends BackboneElement {
  src.daysOfWeek as s -> tgt.availableTime as t, t.daysOfWeek = s;
  src.allDay as s -> tgt.availableTime as t, t.allDay = s;
  src.openingTime : time as s -> tgt.availableTime as t, t.availableStartTime = s;
  src.closingTime : time as s -> tgt.availableTime as t, t.availableEndTime = s;
}

