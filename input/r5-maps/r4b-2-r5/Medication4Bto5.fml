/// url = 'http://hl7.org/fhir/StructureMap/Medication4Bto5'
/// name = 'Medication4Bto5'
/// title = 'FML Conversion for Medication: R4B to R5'

/// status = 'active'

conceptmap "MedicationStatus" {
  prefix s = "http://hl7.org/fhir/4.3/CodeSystem/medication-status"
  prefix t = "http://hl7.org/fhir/CodeSystem/medication-status"

  s:"active" - t:"active"
  s:"inactive" - t:"inactive"
  s:"entered-in-error" - t:"entered-in-error"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/Medication" alias MedicationR4B as source
uses "http://hl7.org/fhir/StructureDefinition/Medication" alias MedicationR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group Medication(source src : MedicationR4B, target tgt : MedicationR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.code -> tgt.code;
  src.status as v -> tgt.status = translate(v, '#MedicationStatus', 'code');
  src.manufacturer -> tgt.marketingAuthorizationHolder;
  src.form -> tgt.doseForm;
  src.amount as s -> tgt.totalVolume = (s.numerator);
  src.ingredient as s -> tgt.ingredient as t then MedicationIngredient(s,t);
  src.batch as s -> tgt.batch as t then MedicationBatch(s,t);
//  src.definition -> tgt.definition;
}

group MedicationIngredient(source src, target tgt) extends BackboneElement {
  src.item : CodeableConcept -> tgt.item;
  src.item : Reference -> tgt.item;
  src.isActive -> tgt.isActive;
  src.strength : Ratio -> tgt.strength;
//  src.strength : CodeableConcept -> tgt.strength;
//  src.strength : Quantity -> tgt.strength;
}

group MedicationBatch(source src, target tgt) extends BackboneElement {
  src.lotNumber -> tgt.lotNumber;
  src.expirationDate -> tgt.expirationDate;
}

