/// url = 'http://hl7.org/fhir/StructureMap/ParameterDefinition4Bto5'
/// name = 'ParameterDefinition4Bto5'
/// title = 'FML Conversion for ParameterDefinition: R4B to R5'

/// status = 'active'


conceptmap "ParameterUse" {
  prefix s = "http://hl7.org/fhir/4.3/operation-parameter-use"
  prefix t = "http://hl7.org/fhir/operation-parameter-use"

  s:"in" - t:"in"
  s:"out" - t:"out"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/ParameterDefinition" alias ParameterDefinitionR4B as source
uses "http://hl7.org/fhir/StructureDefinition/ParameterDefinition" alias ParameterDefinitionR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group ParameterDefinition(source src : ParameterDefinitionR4B, target tgt : ParameterDefinitionR5) extends Element <<type+>> {
  src.name -> tgt.name;
  src.use as v -> tgt.use = translate(v, '#ParameterUse', 'code');
  src.min -> tgt.min;
  src.max -> tgt.max;
  src.documentation -> tgt.documentation;
  src.type as v -> tgt.type = translate(v, 'http://hl7.org/fhir/interversion/types4Bto5', 'code');
  src.profile -> tgt.profile;
}

