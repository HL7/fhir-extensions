/// url = 'http://hl7.org/fhir/StructureMap/Goal4to5'
/// name = 'Goal4to5'
/// title = 'FML Conversion for Goal: R4 to R5'

conceptmap "GoalLifecycleStatus" {
  prefix s = "http://hl7.org/fhir/4.0/goal-status"
  prefix t = "http://hl7.org/fhir/goal-status"

  s:"proposed" - t:"proposed"
  s:"planned" - t:"planned"
  s:"accepted" - t:"accepted"
  s:"active" - t:"active"
  s:"on-hold" - t:"on-hold"
  s:"completed" - t:"completed"
  s:"cancelled" - t:"cancelled"
  s:"entered-in-error" - t:"entered-in-error"
  s:"rejected" - t:"rejected"
}

uses "http://hl7.org/fhir/4.0/StructureDefinition/Goal" alias GoalR4 as source
uses "http://hl7.org/fhir/StructureDefinition/Goal" alias GoalR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group Goal(source src : GoalR4, target tgt : GoalR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.lifecycleStatus as v -> tgt.lifecycleStatus = translate(v, '#GoalLifecycleStatus', 'code');
  src.achievementStatus -> tgt.achievementStatus;
  src.category -> tgt.category;
  src.continuous -> tgt.continuous;
  src.priority -> tgt.priority;
  src.description -> tgt.description;
  src.subject -> tgt.subject;
  src.start : date -> tgt.start;
  src.start : CodeableConcept -> tgt.start;
  src.target as s -> tgt.target as t then GoalTarget(s,t);
  src.statusDate -> tgt.statusDate;
  src.statusReason -> tgt.statusReason;
  src.source -> tgt.source;
  src.addresses -> tgt.addresses;
  src.note -> tgt.note;
  src.outcome -> tgt.outcome;
}

group GoalTarget(source src, target tgt) extends BackboneElement {
  src.measure -> tgt.measure;
  src.detail : Quantity -> tgt.detail;
  src.detail : Range -> tgt.detail;
  src.detail : CodeableConcept -> tgt.detail;
  src.detail : string -> tgt.detail;
  src.detail : boolean -> tgt.detail;
  src.detail : integer -> tgt.detail;
  src.detail : Ratio -> tgt.detail;
  src.due : date -> tgt.due;
  src.due : Duration -> tgt.due;
}

