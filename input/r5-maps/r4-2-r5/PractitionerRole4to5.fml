/// url = 'http://hl7.org/fhir/StructureMap/PractitionerRole4to5'
/// name = 'PractitionerRole4to5'
/// title = 'FML Conversion for PractitionerRole: R4 to R5'

/// status = 'active'

uses "http://hl7.org/fhir/4.0/StructureDefinition/PractitionerRole" alias PractitionerRoleR4 as source
uses "http://hl7.org/fhir/StructureDefinition/PractitionerRole" alias PractitionerRoleR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group PractitionerRole(source src : PractitionerRoleR4, target tgt : PractitionerRoleR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.period -> tgt.period;
  src.practitioner -> tgt.practitioner;
  src.organization -> tgt.organization;
  src.code -> tgt.code;
  src.specialty -> tgt.specialty;
  src.location -> tgt.location;
  src.healthcareService -> tgt.healthcareService;
  src.telecom : ContactPoint as s -> tgt.contact = create('ExtendedContactDetail') as t, t.telecom = s;
//  src.characteristic -> tgt.characteristic;
//  src.communication -> tgt.communication;
  src.availableTime : BackboneElement as s -> tgt.availability as t then PractitionerRoleAvailabilityAvailableTime(s,t) "availableTime";
  src.notAvailable : BackboneElement as s -> tgt.availability as t then PractitionerRoleAvailabilityNotAvailableTime(s,t) "notAvailableTime";
  src.endpoint -> tgt.endpoint;
}

group PractitionerRoleAvailabilityAvailableTime(source src, target tgt) extends BackboneElement {
  src.daysOfWeek as s -> tgt.availableTime as t, t.daysOfWeek = s;
  src.allDay as s -> tgt.availableTime as t, t.allDay = s;
  src.availableStartTime : time as s -> tgt.availableTime as t, t.availableStartTime = s;
  src.availableEndTime : time as s -> tgt.availableTime as t, t.availableEndTime = s;
}

group PractitionerRoleAvailabilityNotAvailableTime(source src, target tgt) extends BackboneElement {
  src.description as s -> tgt.notAvailableTime as t, t.description = s;
  src.during : Period as s -> tgt.notAvailableTime as t, t.during = s;
}

