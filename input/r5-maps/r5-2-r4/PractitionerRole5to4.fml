/// url = 'http://hl7.org/fhir/StructureMap/PractitionerRole5to4'
/// name = 'PractitionerRole5to4'
/// title = 'FML Conversion for PractitionerRole: R5 to R4'

uses "http://hl7.org/fhir/StructureDefinition/PractitionerRole" alias PractitionerRoleR5 as source
uses "http://hl7.org/fhir/4.0/StructureDefinition/PractitionerRole" alias PractitionerRoleR4 as target

imports "http://hl7.org/fhir/StructureMap/*5to4"

group PractitionerRole(source src : PractitionerRoleR5, target tgt : PractitionerRoleR4) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.period -> tgt.period;
  src.practitioner -> tgt.practitioner;
  src.organization -> tgt.organization;
  src.code -> tgt.code;
  src.specialty -> tgt.specialty;
  src.location -> tgt.location;
  src.healthcareService -> tgt.healthcareService;
  src.contact as s -> tgt.telecom = s.telecom "contact";
//  src.characteristic -> tgt.characteristic;
//  src.communication -> tgt.communication;
  src.availability as s -> tgt.availableTime as t then PractitionerRoleAvailabilityAvailableTime(s,t) "availableTime";
  src.availability as s -> tgt.notAvailable as t then PractitionerRoleAvailabilityNotAvailableTime(s,t) "notAvailableTime";
  src.endpoint -> tgt.endpoint;
}

group PractitionerRoleAvailabilityAvailableTime(source src, target tgt) extends BackboneElement {
  src.availableTime as s -> tgt.daysOfWeek as t, t = s.daysOfWeek;
  src.availableTime as s -> tgt.allDay as t, t = s.allDay;
  src.availableTime as s -> tgt.availableStartTime as t, t = s.availableStartTime;
  src.availableTime as s -> tgt.availableEndTime as t, t = s.availableEndTime;
}

group PractitionerRoleAvailabilityNotAvailableTime(source src, target tgt) extends BackboneElement {
  src.notAvailableTime as s -> tgt.description as t, t = s.description;
  src.notAvailableTime as s -> tgt.during as t, t = s.during;
}

